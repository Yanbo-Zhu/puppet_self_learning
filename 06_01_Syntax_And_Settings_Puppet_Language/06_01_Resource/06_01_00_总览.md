
https://www.puppet.com/docs/puppet/8/lang_resources#lang_resources

# 1 文档 

|   |   |
|---|---|
||[https://www.cnblogs.com/liuwei-xd/p/11022742.html](https://www.cnblogs.com/liuwei-xd/p/11022742.html)|
|Resources 官方文档|[https://puppet.com/docs/puppet/7/lang_resources.html#lang_resources](https://puppet.com/docs/puppet/7/lang_resources.html#lang_resources)|
|Resource types 官当文档|[https://puppet.com/docs/puppet/7/resource_types.html](https://puppet.com/docs/puppet/7/resource_types.html)|


# 2 解释 
puppet的操作对象 就是一个个资源 Resource 
Resources are the fundamental unit formodeling system configurations 资源是系统模块配置的基础单位。
向资源类型的属性赋值来实现，可称为资源类型实例化；
定义了资源实例的文件即清单，manifest；

Resources contained in classes and defined types share the relationships of those classes and defined types. Resources are not subject to scope: a resource in any area of code can be referenced from any other area of code.

A resource declaration adds a resource to the catalog and tells Puppet to manage that resource's state.

When Puppet applies the compiled catalog, it:
1. Reads the actual state of the resource on the target system.
2. Compares the actual state to the desired state.
3. If necessary, changes the system to enforce the desired state.
4. Logs any changes made to the resource. These changes appear in Puppet agent's log and in the run report, which is sent to the primary server and forwarded to any specified report processors.

<mark>If the catalog doesn't contain a particular resource, Puppet does nothing with whatever that resource described. If you remove a package resource from your manifests, Puppet doesn't uninstall the package; instead, it just ignores it. To remove a package, manage it as a resource and set `ensure => absent`.</mark>

You can delay adding resources to the catalog. For example, classes and defined types can contain groups of resources. These resources are managed only if you add that class or defined resource to the catalog. Virtual resources are added to the catalog only after they are realized.


----

定义在.pp结尾的资源清单列表文件中

注意：
type必须小写，
title是一个字符串，在同一类型中必须唯一；
每一个属性attribute之间需要用“,”隔开，最后一个“,”可省略。

```
----
type { ‘title’：
attribute1 =>value1，
attribute2 =>value2，
……
}

-------
type{'title':
	attr1 => value1,
	attr2 => value2,
	...
	;
}
```


![[06_01_Syntax_And_Settings_Puppet_Language/06_01_Resource/images/Pasted image 20240411130832.png]]

TITLE : 
The TITLE is the name that puppet uses to identify the resource internally. E
very resource must have a unique title. 
With file resources this is typically the full path to the file (which is therefore unique by nature).
Beware the title field (and other fields) in your manifest can be case sensitive.

Attr
表示资源的属性，=>符合是给属性赋值
资源属性中的三个特殊属性： 
- Namwvar：可简单称为 name(name可省略，此时将由title表示)；
- ensure属性：用来定义资源的目标状态，不同的资源ensure值不同。比如，user资源中ensure可以设置为present表示用户必须存在；设置为absent表示用户不能存在（删除用户） 但是在service资源中，ensure的值往往被设置为stopped或者running。大部分是前者。
- Provider：指明资源的管理接口；


# 3 Resource declarations

At minimum, every resource declaration has a resource type, a title, and a set of attributes:

```
<TYPE> { '<TITLE>': <ATTRIBUTE> => <VALUE>, }
```

The resource title and attributes are called the resource body. A resource declaration can have one resource body or multiple resource bodies of the same resource type.



# 4 Resource uniqueness

Each resource must be unique; Puppet does not allow you to declare the same resource twice. This is to prevent multiple conflicting values from being declared for the same attribute. Puppet uses the resource `title` and the `name` attribute or namevar to identify duplicate resources — if either the title or the name is duplicated within a given resource type, catalog compilation fails. See the page about [resource syntax](https://www.puppet.com/docs/puppet/8/lang_resources#lang_resource_syntax "You can accomplish a lot with just a few resource declaration features, or you can create more complex declarations that do more.") for details about resource titles and namevars. To provide the same resource for multiple classes, use a class or a virtual resource to add it to the catalog in multiple places without duplicating it. See [classes](https://www.puppet.com/docs/puppet/8/lang_classes#lang_classes "Classes are named blocks of Puppet code that are stored in modules and applied later when they are invoked by name. You can add classes to a node’s catalog by either declaring them in your manifests or assigning them from an external node classifier (ENC). Classes generally configure large or medium-sized chunks of functionality, such as all of the packages, configuration files, and services needed to run an application.") and [virtual resources](https://www.puppet.com/docs/puppet/8/lang_virtual "A virtual resource declaration specifies a desired state for a resource without enforcing that state. Puppet manages the resource by realizing it elsewhere in your manifests. This divides the work done by a normal resource declaration into two steps. Although virtual resources are declared one time, they can be realized any number of times, similar to a class.") for more information.


# 5 Title

A resource's title is a string that uniquely identifies the resource to Puppet. In a resource declaration, the title is the identifier after the first curly brace and before the colon. For example, in this file resource declaration, the title is `/etc/passwd`:

```
file  { '/etc/passwd':
  owner => 'root',
  group => 'root',
}
```

Titles must be unique per resource type. You can have both a package and a service titled "ntp," but you can only have one service titled "ntp." Duplicate titles cause compilation to fail.

The title of a resource differs from the namevar of the resource. Whereas the title identifies the resource to Puppet itself, the namevar identifies the resource to the target system and is usually specified by the resource's `name` attribute. The resource title doesn't have to match the namevar, but you'll often want it to: the value of the namevar attribute defaults to the title, so using the name in the title can save you some typing.

If a resource type has multiple namevars, the type specifies whether and how the title maps to those namevars. For example, the `package` type uses the `provider` attribute to help determine uniqueness, but that attribute has no special relationship with the title. See each type's documentation for details about how it maps title to namevars.


# 6 Resource types

Every resource is associated with a resource type, which determines the kind of configuration it manages. Puppet has built-in resource types such as `file`, `service`, and `package`. See the [resource type reference](https://www.puppet.com/docs/puppet/8/type) for a complete list and information about the built-in resource types.

You can also add new resource types to Puppet:

- Defined types are lightweight resource types written in the Puppet language.
    
- Custom resource types are written in Ruby and have the same capabilities as Puppet's built-in types.

# 7 Resource syntax

## 7.1 Basic syntax




-----


The simplified form of a resource declaration includes:

- The resource type, which is a lowercase word with no quotes, such as `file`.
    
- An opening curly brace `{`.
    
- The title, which is a string.
    
- A colon (`:`).
    
- Optionally, any number of attribute and value pairs, each of which consists of:
    
    - An attribute name, which is a lowercase word with no quotes.
        
    - A `=>` (called an arrow, "fat comma," or "hash rocket").
        
    - A value, which can have any [data type][datatype].
        
    - A trailing comma.
        
- A closing curly brace (`}`).
    

You can use any amount of whitespace in the Puppet language.

This example declares a file resource with the title `/etc/passwd`. This declaration's `ensure` attribute ensures that the specified file is created, if it does not already exist on the node. The rest of the declaration sets values for the file's `owner`, `group`, and `mode` attributes.

```
file { '/etc/passwd':
  ensure => file,
  owner  => 'root',
  group  => 'root',
  mode   => '0600',
}
```


## 7.2 Complete syntax

By creating more complex resource declarations, you can:

- Describe many resources at once.
    
- Set a group of attributes from a hash with the `*` attribute.
    
- Set default attributes.
    
- Specify an abstract resource type.
    
- Amend or override attributes after a resource is already declared.
    

The complete generalized form of a resource declaration expression is:

- The resource type, which can be one of:
    
    - A lowercase word with no quotes, such as `file`.
        
    - A resource type data type, such as `File`, `Resource[File]`, or `Resource['file']`. It must have a type but not a resource reference.
        
- An opening curly brace (`{`).
    
- One or more resource bodies, separated with semicolons (`;`). Each resource body consists of:
    
    - A title, which can be one of:
        
        - A string.
            
        - An array of strings, which declares multiple resources.
            
        - The special value `default`, which sets default attribute values for other resource bodies in the same expression.
            
    - A colon (`:`).
        
    - Optionally, any number of attribute and value pairs, separated with commas (`,`). Each attribute/value pair consists of:
        
        - An attribute name, which can be one of:
            
            - A lowercase word with no quotes.
                
            - The special attribute `*`, called a "splat," which takes a hash and sets other attributes.
                
            - A `=>`, called an arrow, a "fat comma," or a "hash rocket".
                
            - A value, which can have any data type.
                
        - Optionally, a trailing comma after the last attribute/value pair.
            
- Optionally, a trailing semicolon after the last resource body.
    
- A closing curly brace (`}`)
    

```
<TYPE> 
      { default: * => <HASH OF ATTRIBUTE/VALUE PAIRS>, <ATTRIBUTE> => <VALUE>, ; 
                      '<TITLE>': * => <HASH OF ATTRIBUTE/VALUE PAIRS>, <ATTRIBUTE> => <VALUE>, ; 
                      '<NEXT TITLE>': ... ; 
                      ['<TITLE'>, '<TITLE>', '<TITLE>']: ... ; 
      } 
```



## 7.3 Resource declaration default attributes

default value 的给入 

If a resource declaration includes a resource body with a title of `default`, Puppet doesn't create a new resource named "default." Instead, every other resource in that declaration uses attribute values from the `default` body if it doesn't have an explicit value for one of those attributes. This is also known as "per-expression defaults."

Resource declaration defaults are useful because it lets you set many attributes at once, but you can still override some of them.

This example declares several different files, all using the default values set in the `default` resource body. However, the `mode` value for the the files in the last array (`['ssh_config', 'ssh_host_dsa_key.pub'....`) is set explicitly instead of using the default.

```
file {
  default:
    ensure => file,
    owner  => "root",
    group  => "wheel",
    mode   => "0600",
  ;
  ['ssh_host_dsa_key', 'ssh_host_key', 'ssh_host_rsa_key']:
    # use all defaults
  ;
  ['ssh_config', 'ssh_host_dsa_key.pub', 'ssh_host_key.pub', 'ssh_host_rsa_key.pub', 'sshd_config']:
    # override mode
    mode => "0644",
  ;
}
```

The position of the `default` body in a resource declaration doesn't matter; resources above and below it all use the default attributes if applicable.  You can only have one `default` resource body per resource declaration.


## 7.4 Setting attributes from a hash

You can set attributes for a resource by using the splat attribute, which uses the splat or asterisk character `*`, in the resource body.

The value of the splat (`*`) attribute must be a hash where:
- Each key is the name of a valid attribute for that resource type, as a string.
- Each value is a valid value for the attribute it's assigned to.

This sets values for that resource's attributes, using every attribute and value listed in the hash.

For example, the splat attribute in this declaration sets the `owner`, `group`, and `mode` settings for the file resource.

```
$file_ownership = {
  "owner" => "root",
  "group" => "wheel",
  "mode"  => "0644",
}

file { "/etc/passwd":
  ensure => file,
  *      => $file_ownership,
}
```

You cannot set any attribute more than once for a given resource; if you try, Puppet raises a compilation error. This means that:

- If you use a hash to set attributes for a resource, you cannot set a different, explicit value for any of those attributes. For example, if `mode` is present in the hash, you can't also set `mode => "0644"` in that resource body.
    
- You can't use the `*` attribute multiple times in one resource body, since the splat itself is an attribute.
    

To use some attributes from a hash and override others, either use a hash to set per-expression defaults, as described in the **Resource declaration default attributes** section (above), or use the merging operator, `+` to combine attributes from two hashes, with the right-hand hash overriding the left-hand one.


## 7.5 Abstract resource types

Because a resource declaration can accept a resource type data type as its resource type , you can use a `Resource[<TYPE>]` value to specify a non-literal resource type, where the `<TYPE>` portion can be read from a variable. That is, the following three examples are equivalent to each other:

```
file { "/tmp/foo": ensure => file, } 
File { "/tmp/foo": ensure => file, } 
Resource[File] { "/tmp/foo": ensure => file, }
```

```
$mytype = File
Resource[$mytype] { "/tmp/foo": ensure => file, }
```

```
$mytypename = "file"
Resource[$mytypename] { "/tmp/foo": ensure => file, }
```

This lets you declare resources without knowing in advance what type of resources they'll be, which can enable transformations of data into resources.

## 7.6 Arrays of titles

If you specify an array of strings as the title of a resource body, Puppet creates multiple resources with the same set of attributes. This is useful when you have many resources that are nearly identical.

For example:

```
$rc_dirs = [
  '/etc/rc.d',       '/etc/rc.d/init.d','/etc/rc.d/rc0.d',
  '/etc/rc.d/rc1.d', '/etc/rc.d/rc2.d', '/etc/rc.d/rc3.d',
  '/etc/rc.d/rc4.d', '/etc/rc.d/rc5.d', '/etc/rc.d/rc6.d',
]

file { $rc_dirs:
  ensure => directory,
  owner  => 'root',
  group  => 'root',
  mode   => '0755',
}
```

If you do this, you must let the namevar attributes of these resources default to their titles. You can't specify an explicit value for the namevar, because it applies to all of the resources.